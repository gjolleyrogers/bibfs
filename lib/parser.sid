%types%

	string;
	field;
	entry;

%terminals%

	!error;

	eof;
	!nl;
	equ;
	comma;
	at;
	cat;
	obrace;
	cbrace;

	str:  () -> (:string);
	word: () -> (:string);

%productions%

	<free>:   (:string) -> ();
	<strcat>: (:string, :string) -> (:string);
	<lookup>: (:string) -> (:string);

	<new-field>: (:string, :string) -> (:field);
	<add-field>: (:field, :field) -> (:field);
	<null-field>: () -> (:field);

	<new-entry>: (:string, :string, :field) -> (:entry);
	<add-entry>: (:entry, :entry) -> (:entry);
	<null-entry>: () -> (:entry);

	str-or-var: () -> (v :string) = {
		v = str;
	||
		w = word;
		v = <lookup>(w);
	};

	cats: () -> (v :string) = {
		a = str-or-var;
		cat;
		b = cats;
		v = <strcat>(a, b);
	||
		v = str-or-var;
	};

	fields: () -> (f :field) [

		field: () -> (f :field) = {
			n = word; /* name */
			equ;
			v = cats; /* value */

			f = <new-field>(n, v);

			<free>(n);
			<free>(v);
		};

	] = {
		a = field;
		{
			f = a;
		||
			comma;
			b = fields;
			f = <add-field>(a, b);
		};
	||
		f = <null-field>;
	};

	entries: () -> (e :entry) [

		entry: () -> (e :entry) = {
			at;
			t = word; /* type */
			obrace;
			k = word; /* key */
			comma;

			f = fields;

			cbrace;

			e = <new-entry>(t, k, f);

			/* t is owned by the entry */
			<free>(k);

		/* ## TODO: panic until next @. log paniced-over bytes somewhere */
		};

	] = {
		a = entry;
		{
			e = a;
		||
			b = entries;
			e = <add-entry>(a, b);
		};
	};

	bib: () -> (e :entry) = {
		{
			e = entries;
		||
			e = <null-entry>;
		};
		eof;
	};

%entry%

	bib;

